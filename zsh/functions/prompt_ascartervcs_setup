# Developer zsh prompt with vcs theme
# Andrew Carter <ascarter@gmail.com>
# 
# Developer prompt with VCS info
#

prompt_ascartervcs_help () {
    cat <<'EOH'

Developer prompt with VCS info

EOH
}

prompt_ascartervcs_setup () {
    # Set some color vars
    local reset="%{${reset_color}%}"
    for COLOR in white red green black blue yellow magenta; do
        eval local $COLOR='%{$fg[${COLOR}]%}'
        eval local ${COLOR}_bold='%{$fg_bold[${COLOR}]%}'
    done

    zstyle ':vcs_info:*' enable git svn cvs hg bzr
    zstyle ':vcs_info:*' get-revision true
    zstyle ':vcs_info:*' check-for-changes true
    
    zstyle ':vcs_info:*' actionformats "[%s %b (%a) %i]%m%c%u"
    zstyle ':vcs_info:*' formats "[%s %b%m %i]%m%c%u"
    zstyle ':vcs_info:git*' actionformats "[%s %b (%a) %12.12i]%m%c%u"
    zstyle ':vcs_info:git*' formats "[%s %b %12.12i]%m%c%u"
    
    zstyle ':vcs_info:(git|hg|svn):*' branchformat '%b'
    zstyle ':vcs_info:*' stagedstr "${green_bold}%B+%b${reset}"
    zstyle ':vcs_info:*' unstagedstr "${red_bold}%B?%b${reset}"
    
    # Mark untracked files
#     zstyle ':vcs_info:git*+set-message:*' hooks git-untracked  
#     +vi-git-untracked(){
#         if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]]; then
#             git_untracked
#             git_st
#         fi
#     }
    
    git_untracked() {
        if $(git status --porcelain | grep '??' &> /dev/null); then
            hook_com[unstaged]+="?"
        fi
    }
    
    git_st() {
        local ahead behind
        local -a gitstatus
        
        ahead=$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)
        (( $ahead )) && gitstatus+=( "+${ahead:t:gs/ /}" )
        
        behind=$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)
        (( $behind )) && gitstatus+=( "-${behind:t:gs/ /}" )
        
        hook_com[misc]+=${(j:/:)gitstatus}
    }

    precmd () { vcs_info }
    prompt_opts=( cr percent subst )
    PROMPT='
${black_bold}%n@%m${reset}:%B%~%b${reset}
%# '
    PROMPT4='+%N:%i:%_>'    
    RPROMPT='%{$fg_bold[black]%}${vcs_info_msg_0_}%{$reset_color%}'
}

_filter_vcsinfo() { '${(S)vcs_info_msg_0_//(\%b|\%B)/}' }

prompt_ascartervcs_setup "$@"
