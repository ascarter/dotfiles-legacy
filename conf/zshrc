fpath=(${ZDOTDIR:-$DOTFILES/zsh}/completion ${ZDOTDIR:-$DOTFILES/zsh}/function ${ZDOTDIR:-$DOTFILES/zsh}/prompt $fpath)

# Check for homebrew
if [ -d /opt/homebrew ]; then
  export HOMEBREW_NO_EMOJI=1
  export HOMEBREW_NO_ANALYTICS=1
  eval $(/opt/homebrew/bin/brew shellenv)
  fpath+=($(brew --prefix)/share/zsh/site-functions $(brew --prefix)/share/zsh-completions)
fi

autoload -Uz compinit
compinit -u

autoload -U promptinit
promptinit

autoload -U colors
colors

autoload -U ${ZDOTDIR:-$DOTFILES/zsh}/function/[^_]*(:t)
autoload add-zsh-hook

# Support bash completions
autoload bashcompinit
bashcompinit

# Enable vcs info
autoload -Uz vcs_info

# ===========
# Prompt
# ===========

# Default: PS1="%m%# "
declare +x PS1
prompt vcs

# ========================================
# Shell preferences
# ========================================

# Retain history across multiple zsh sessions
HISTFILE=${ZDOTDIR:-$HOME}/.zsh_history
setopt SHARE_HISTORY
setopt APPEND_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
SAVEHIST=5000
HISTSIZE=2000

# Key mappings

# Emacs key mappings
bindkey -e

# Forward delete
bindkey "^[[3~" delete-char

# Editor
export EDITOR="vim"
export VISUAL="vim -g"
export LESSEDIT='vim ?lm+%lm. %f'
export TEXEDIT='vim +%d %s'

# less
export PAGER=less
export LESS="--status-column --long-prompt --no-init --quit-if-one-screen --quit-at-eof -R"

# 1Password
if (( $+commands[op] )); then
  eval "$(op completion zsh)"
  compdef _op op
fi

# ========================================
# Developer Tools
# ========================================

# SDK root for custom installs
export SDK_ROOT=${HOME}/sdk

# GitHub
if (( $+commands[gh] )); then
  eval "$(gh completion -s zsh)"
fi

# Ruby
if (( $+commands[ruby] )) && (( $+commands[gem] )); then
  path+=$(ruby -r rubygems -e 'puts Gem.user_dir')/bin
fi

# Python
case $(uname) in
Darwin )
  # Installed Python
  path+=/Library/Frameworks/Python.framework/Versions/Current/bin

  # User pip installed binaries are in ~/Library on macOS
  local pyver=$(python3 -c "import sys; print ('{}.{}'.format(sys.version_info.major, sys.version_info.minor))")
  if [[ -d ${HOME}/Library/Python/${pyver} ]]; then
      export LC_ALL=en_US.UTF-8
      export LANG=en_US.UTF-8
      path+=${HOME}/Library/Python/${pyver}/bin
  fi
esac

# User pip installed binaries are in ~/.local/bin on Linux
if [[ -d ${HOME}/.local/bin ]]; then
    path+=${HOME}/.local/bin
fi

# Pip 3
if (( $+commands[pip3] )); then
  source <(pip3 completion --zsh)
  compctl -K _pip_completion pip3
fi

# Pipx
if (( $+commands[pipx] )); then
  eval "$(register-python-argcomplete pipx)"
fi


# Node.js
if (( $+commands[npm] )); then
  source <(npm completion)
fi

# Go
path=(/usr/local/go/bin $path)

# Add GOPATH to installed binaries to path
if (( $+commands[go] )); then
  path=($(go env GOPATH)/bin $path)
fi

# .NET Core
if (( $+commands[dotnet] )); then
  # Disable telemtry
  DOTNET_CLI_TELEMETRY_OPTOUT=1

  # Add dotnet tools
  path+=${HOME}/.dotnet/tools

  # zsh parameter completion for the dotnet CLI
  _dotnet_zsh_complete() {
      local completions=("$(dotnet complete "$words")")
      reply=( "${(ps:\n:)completions}" )
  }
  compctl -K _dotnet_zsh_complete dotnet
fi

# PowerShell
if (( $+commands[pwsh] )); then
  # Disable telemetry
  POWERSHELL_TELEMETRY_OPTOUT=1
fi

# Java
if [[ -e /usr/libexec/java_home ]]; then
  # Verify that java is installed
  /usr/libexec/java_home > /dev/null 2>&1
  if [[ $? -eq 0 ]]; then
    # Java installed - use the default JDK
    export JAVA_HOME=`/usr/libexec/java_home`
  fi
else
    export JAVA_HOME=$(readlink -f `whence -cp java` | sed "s:bin/java::")
fi

# Android
if [[ -d ${HOME}/Library/Android/sdk ]]; then
  export ANDROID_HOME=${HOME}/Library/Android/sdk
  path+=(${ANDROID_HOME}/tools ${ANDROID_HOME}/tools/bin ${ANDROID_HOME}/platform-tools)
fi

# Flutter
if [[ -d ${SDK_ROOT}/flutter ]]; then
  export FLUTTER_ROOT=${SDK_ROOT}/flutter
  path+=${FLUTTER_ROOT}/bin
fi

# Rust
if [[ -d ${HOME}/.cargo ]]; then
  source ${HOME}/.cargo/env
fi

# Kubernetes
if (( $+commands[kubectl] )); then
  source <(kubectl completion zsh)
fi

# ========================================
# Aliases
# ========================================

if [ -f ${ZDOTDIR:-$DOTFILES/zsh}/zsh_aliases ]; then
  source ${ZDOTDIR:-$DOTFILES/zsh}/zsh_aliases
fi

# ========================================
# Path settings
# ========================================

# Add home bin dir if it is there
path+=${DOTFILES}/bin

# ========================================
# SSH
# ========================================

# Use 1Password SSH Agent if installed
if [ -S ~/.1password/agent.sock ]; then
  export SSH_AUTH_SOCK=~/.1password/agent.sock
else
  case $(uname) in
  Linux )
    # WSL - use named pipe to Windows host ssh-agent
    if [ -n "${WSL_DISTRO_NAME}" ] && type npiperelay.exe &>/dev/null; then
      export SSH_AUTH_SOCK=${HOME}/.ssh/agent.sock
      ss -a | grep -q $SSH_AUTH_SOCK
      if [ $? -ne 0 ]; then
        rm -f ${SSH_AUTH_SOCK}
        ( setsid socat UNIX-LISTEN:${SSH_AUTH_SOCK},fork EXEC:"npiperelay.exe -ei -s //./pipe/openssh-ssh-agent",nofork & ) >/dev/null 2>&1
      fi
    elif (( $+commands[keychain] )); then
      # Use keychain if installed
      eval `keychain --eval --agents ssh id_rsa id_ed25519`
    fi
    ;;
  esac
fi

# ========================================
# Per-machine extras
# ========================================
[[ -e ${HOME}/.zsh_local ]] && source ${HOME}/.zsh_local


# ========================================
# Banners and messages
# ========================================

case $(uname) in
Linux )
  if [ -x "$(command -v show-motd)" ] && show-motd login
  ;;
esac
